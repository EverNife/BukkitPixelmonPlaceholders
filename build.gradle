allprojects {
    buildscript {
        repositories {
            maven { url = 'https://maven.minecraftforge.net/' }
            jcenter()
            mavenCentral()
        }
    }

    apply plugin: 'java'

    group 'br.com.finalcraft'
    version '1.0.8'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = 'UTF-8'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
        mavenCentral()
        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }
        maven {
            url = "https://maven.petrus.dev/public"
        }
    }

    dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
        compileOnly "br.com.finalcraft:EverNifeCore:2.0.3"

        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

        //Annotations
        compileOnly 'org.jetbrains:annotations:24.0.0'

        //Lombok
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
        testCompileOnly 'org.projectlombok:lombok:1.18.24'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
    }
}

buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

apply plugin: 'maven-publish'
apply plugin: "com.github.johnrengelman.shadow"

def dependencyFile = project.file("libs/v1_16_5-${version}.jar")
dependencies {
    implementation project("pixelmon:commons")

    if (dependencyFile.exists()){
        implementation name: "v1_16_5-${version}"
    }
}

//Code to build sub projets separated from the main project, to keep Forge Mappings correct
task buildRequiredSubprojects(type: Copy) {
    dependsOn ":pixelmon:v1_16_5:build"
    from "$rootDir/pixelmon/v1_16_5/build/libs/"
    into "$rootDir/libs"
}

jar {
    enabled = false //Disable default jar, only shadow jar will be created
    dependsOn(shadowJar { classifier = null })
}

shadowJar {
    archiveName = "${baseName}-${version}.${extension}"
}

publishing {
    repositories {
        maven {
            name = "PetrusRepo"
            url = "https://maven.petrus.dev/public"
            credentials {
                username = System.env.PETRUSMAVEN_ACTOR
                password = System.env.PETRUSMAVEN_TOKEN
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        petrus(MavenPublication) {
            from(components.java)
            pom.withXml {
                asNode().appendNode('url','https://petrus.dev')
                asNode().dependencies.'*'.each() {
                    it.parent().remove(it) //Remove all dependencies from pom file
                }
            }
        }
    }
}